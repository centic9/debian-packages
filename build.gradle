apply plugin: 'java'

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_17
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.dstadler:commons-dost:1.4.4'
    implementation 'org.kohsuke:github-api:1.330'
    implementation 'com.google.guava:guava:33.5.0-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'

    testImplementation 'org.dstadler:commons-test:1.0.0.22'
}

wrapper {
    gradleVersion = '8.14.3'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

def gitHubToken = new ByteArrayOutputStream()
tasks.register('getGitHubToken', Exec) {
    description = 'Try to get GITHUB_TOKEN from the password managed'

    workingDir '.'
    commandLine 'bash', '-c', 'pass ls Programming/GitHub | grep Token-Repository | sed -e \'s/Token-Repository: //g\''
    standardOutput = gitHubToken;
}

tasks.register('downloadPackages', JavaExec) {
    description = 'Fetch built packages from the GitHub Action artifacts'

    environment "GITHUB_TOKEN", gitHubToken
    mainClass = 'org.dstadler.github.packaging.DownloadPackages'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ['-Xmx256m']
}

tasks.register('processPackages', Exec) {
    description = 'Add packages to the local APT repository'

    workingDir '.'
    commandLine 'bash', './processPackages.sh'
}

downloadPackages.mustRunAfter getGitHubToken
processPackages.mustRunAfter downloadPackages

tasks.register('download') {
    dependsOn getGitHubToken, downloadPackages, processPackages
}
